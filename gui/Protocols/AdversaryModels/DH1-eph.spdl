/* 
 * DH1-eph
 *
 * IEEE P1363
 *
 */

// Hash functions
hashfunction h1,h2,g1,g2;
hashfunction KDF;

protocol @publickeys(RA)
{
	role RA
	{
		send_!1(RA,RA, g1(sk(RA)));
	}
}

/*
 * Hack to simulate g^ab = g^ba.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA,RB)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, g2(g1(T1),T2) );
		send_!2(RA,RA, g2(g1(T2),T1) );
	}
	role RB
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!3(RB,RB, KDF( g2(g1(T1),T2)) );
		send_!4(RB,RB, KDF( g2(g1(T2),T1)) );
	}
}

// The protocol description

symmetric-role protocol DH1-eph(I,R)
{
	role I
	{
		const x: Nonce;
		var beta: Ticket;

		send_1(I,R,  g1(x) );
		recv_2(R,I,  beta );

		claim(I,SKR, KDF(g2(beta,x)) );
	}	
	
	role R
	{
		const y: Nonce;
		var alpha: Ticket;

		recv_1(I,R,  alpha );
		send_2(R,I,  g1(y) );

		claim(R,SKR, KDF(g2(alpha,y)) );
	}
}


