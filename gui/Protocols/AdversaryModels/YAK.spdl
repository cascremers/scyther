/* 
 * YAK protocol
 * Hao
 *
 */

// Hash functions
hashfunction h1,h2,exp;
hashfunction KDF;

hashfunction H,HE,add,mult;

usertype group;
usertype string;

const g: group;

/*
 * Hack to simulate g^ab = g^ba.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA,RB,RC)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, exp(exp(g,T1),T2) );
		send_!2(RA,RA, exp(exp(g,T2),T1) );
	}
	role RB
	{
		var alpha,beta, T1,T2,T3: Ticket;

		recv_!3(RB,RB, H( exp(exp(g,T1),T2),T3) );
		send_!4(RB,RB, H( exp(exp(g,T2),T1),T3) );
	}
	role RC
	{
		var alpha,beta, T1,T2,T3: Ticket;

		recv_!5(RC,RC, H( T3,exp(exp(g,T1),T2) ) );
		send_!6(RC,RC, H( T3,exp(exp(g,T2),T1) ) );
	}
}

protocol @addmult(RA,RB,RC)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, add(T1,T2) );
		send_!2(RA,RA, add(T2,T1) );
	}
	role RB
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!3(RB,RB, mult(T1,T2) );
		send_!4(RB,RB, mult(T2,T1) );
	}
	role RC
	{
		var x,y: Nonce;

		recv_!5(RC,RC, 
		  H(
		    mult(
		      exp(exp(g,y),add(x,sk(RA))),
		      exp(exp(g,sk(RB)),add(x,sk(RA)))
		    )
		   )
		  );
		send_!6(RC,RC,
		  H(
		    mult(
		      exp(exp(g,x),add(y,sk(RB))),
		      exp(exp(g,sk(RA)),add(y,sk(RB)))
		    )
		   )
		  );
	}
}

// The protocol description

symmetric-role protocol YAK(A,B)
{
	role A
	{
		fresh x: Nonce;
		var Y: Ticket;

		send_1(A,B,  exp(g,x) );
		recv_2(B,A,  Y );

		claim(A,SKR,
		  H(
		    mult(
		      exp(Y,add(x,sk(A))),
		      exp(exp(g,sk(B)),add(x,sk(A)))
		    )
		   )
		  );
	}	
	
	role B
	{
		fresh y: Nonce;
		var X: Ticket;

		recv_1(A,B,  X );
		send_2(B,A,  exp(g,y) );

		claim(B,SKR,
		  H(
		    mult(
		      exp(X,add(y,sk(B))),
		      exp(exp(g,sk(A)),add(y,sk(B)))
		    )
		   )
		  );
	}
}


